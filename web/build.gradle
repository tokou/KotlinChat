buildscript {

    repositories {
        jcenter()
        maven { url "https://dl.bintray.com/kotlin/kotlin-eap" }
    }

    dependencies {
        classpath "org.jetbrains.kotlin:kotlin-frontend-plugin:$kotlin_frontend_plugin_version"
    }
}

apply plugin: 'kotlin2js'
apply plugin: 'kotlinx-serialization'
// Disable DCE for until we figure out how to make it work with the tests
// apply plugin: 'kotlin-dce-js'
apply plugin: 'org.jetbrains.kotlin.frontend'

repositories {
    maven { url 'https://dl.bintray.com/kotlin/kotlin-js-wrappers' }
}

dependencies {
    compile project(':js')

    compile "org.jetbrains.kotlin:kotlin-stdlib-js:$kotlin_version"
    compile "org.jetbrains.kotlinx:kotlinx-serialization-runtime-js:$kotlin_serialization_version"
    compile "org.jetbrains.kotlinx:kotlinx-coroutines-core-js:$kotlin_coroutines_version"

    compile "org.jetbrains:kotlin-react:$kotlin_react_version"
    compile "org.jetbrains:kotlin-react-dom:$kotlin_react_version"
    compile "org.jetbrains:kotlin-extensions:$kotlin_extensions_version"
    compile "org.jetbrains.kotlinx:kotlinx-html-js:$kotlin_html_js_version"

    // Needs to be in 'compile' for karma tests to work
    compile "org.jetbrains.kotlin:kotlin-test-js:$kotlin_version"
}

[compileKotlin2Js, compileTestKotlin2Js]*.configure {
    kotlinOptions {
        metaInfo = true
        sourceMap = true
        sourceMapEmbedSources = 'always'
        moduleKind = 'commonjs'
    }
}

// Disable DCE for until we figure out how to make it work with the tests
//runDceKotlinJs.dceOptions.devMode = true
//runDceTestKotlinJs.dceOptions.devMode = true

kotlinFrontend {

    sourceMaps = true

    npm {
        dependency("react", "^16.2.0")
        dependency("react-dom", "^16.2.0")

        devDependency "karma"
        devDependency "mocha"
        devDependency "enzyme"
        devDependency "@types/enzyme"
        devDependency "enzyme-adapter-react-16"
        devDependency "@types/enzyme-adapter-react-16"
        devDependency "karma-chrome-launcher"
        devDependency "karma-mocha-reporter"
    }

    webpackBundle {
        bundleName = "main"
        contentPath = file('src/main/resources')
        sourceMaps = true
    }

    karma {
        reporters = ['mocha', 'junit']
        frameworks = ['mocha']
        browsers = ['Chrome']
        plugins = ['karma-chrome-launcher', 'karma-mocha-reporter']
        sourceMaps = true
    }
}

tasks.whenTaskAdded { t ->
    if (t.name == "karma-run-single") {
        t.outputs.upToDateWhen { false }
    }
}

kotlin {
    experimental {
        coroutines "enable"
    }
}
